import QtQuick
import Quickshell
import Quickshell.Io
import "Model.js" as Model

Item {
  id: root
  property var settings: ({})

  property bool api: false
  property bool altSpeed: false
  property real dlSpeed: 0
  property real upSpeed: 0
  property var torrents: []
  property var files: []
  property string lastError: ""
  property string actionStatus: ""
  property string clipboardText: ""
  property bool initialized: false
  property string statusConnectionKey: ""
  property string actionConnectionKey: ""
  property string actionName: ""
  property string filesConnectionKey: ""
  property var actionQueue: []
  property var notifyQueue: []
  property var magnetInbox: []
  property var magnetPending: []
  property bool magnetHandlerInstalled: false
  property double magnetBackoffUntil: 0
  property bool magnetDrainQueued: false
  property bool magnetNotReadyNotified: false

  readonly property string baseUrl: {
    var value = String(settings && settings.baseUrl != null ? settings.baseUrl : "").trim()
    return value === "" ? "http://127.0.0.1:8080" : value
  }
  readonly property string username: String(settings && settings.username != null ? settings.username : "")
  readonly property string connectionKey: baseUrl + "\u0000" + username
  readonly property int refreshIntervalSec: {
    var n = parseInt(String(settings && settings.refreshIntervalSec != null ? settings.refreshIntervalSec : 5), 10)
    if (!isFinite(n)) n = 5
    if (n < 5) n = 5
    if (n > 3600) n = 3600
    return n
  }
  readonly property string helperPath: {
    var value = Qt.resolvedUrl("qbt").toString()
    if (value.indexOf("file://") === 0) return decodeURIComponent(value.substring(7))
    return value
  }
  readonly property var magnetPendingHashes: {
    var out = []
    var p = magnetPending || []
    for (var i = 0; i < p.length; i++) {
      if (p[i] && p[i].hash) out.push(p[i].hash)
      var hs = (p[i] && p[i].hashes) || []
      for (var j = 0; j < hs.length; j++) if (hs[j]) out.push(hs[j])
    }
    return out
  }
  readonly property bool magnetWatching: (magnetInbox && magnetInbox.length > 0) || (magnetPending && magnetPending.length > 0)
  readonly property bool busy: statusProcess.running || actionProcess.running || filesProcess.running || clipProcess.running || actionQueue.length > 0
  readonly property bool ready: api
  readonly property bool transferring: Model.anyActive(torrents, magnetPendingHashes)
  readonly property bool warning: !api

  function helperCommand(name, args) {
    var command = [helperPath, "--base-url", baseUrl, "--username", username, name]
    var values = args || []
    for (var i = 0; i < values.length; i++) command.push(String(values[i]))
    return command
  }

  function clearError() { lastError = "" }

  function clearRemoteState() {
    api = false
    altSpeed = false
    dlSpeed = 0
    upSpeed = 0
    torrents = []
    files = []
  }

  function connectionChanged() {
    clearRemoteState()
    lastError = ""
    actionStatus = ""
    refresh()
  }

  function applyStatus(raw) {
    var parsed = Model.parseStatusJson(raw)
    if (!parsed.ok) {
      clearRemoteState()
      lastError = "Failed to read qBittorrent status"
      return
    }
    var finished = parsed.api ? Model.newlyCompleted(
      Model.excludePending(torrents, magnetPendingHashes),
      Model.excludePending(parsed.torrents, magnetPendingHashes)
    ) : []
    api = parsed.api
    altSpeed = parsed.api ? parsed.altSpeed : false
    dlSpeed = parsed.api ? parsed.dlSpeed : 0
    upSpeed = parsed.api ? parsed.upSpeed : 0
    torrents = parsed.api ? parsed.torrents : []
    if (!parsed.api) files = []
    lastError = Model.nextStatusError(parsed, lastError)
    if (finished.length > 0) notify(Model.completionText(finished))
  }

  function notify(text) {
    if (!text) return
    if (notifyProcess.running) {
      notifyQueue = Model.enqueueAction(notifyQueue, { text: String(text) })
      return
    }
    notifyProcess.command = ["notify-send", "-a", "OmaqBT", "OmaqBT", String(text)]
    notifyProcess.running = true
  }

  function pumpNotifyQueue() {
    var next = Model.shiftAction(notifyQueue)
    notifyQueue = next.rest
    if (next.item && next.item.text) notify(next.item.text)
  }

  function runAction(name, args, status) {
    var item = {
      name: String(name),
      cmd: helperCommand(name, args),
      status: status || "",
      connectionKey: connectionKey
    }
    if (actionProcess.running) {
      actionQueue = Model.enqueueAction(actionQueue, item)
      return true
    }
    startQueuedAction(item)
    return true
  }

  function startQueuedAction(item) {
    if (!item || !item.cmd) return
    clearError()
    actionStatus = item.status || ""
    actionConnectionKey = item.connectionKey || connectionKey
    actionName = item.name || ""
    actionProcess.command = item.cmd
    actionProcess.running = true
  }

  function pumpActionQueue() {
    var next = Model.shiftAction(actionQueue)
    actionQueue = next.rest
    if (next.item) startQueuedAction(next.item)
  }

  function refresh() {
    if (statusProcess.running) return
    statusConnectionKey = connectionKey
    statusProcess.command = helperCommand("status", [])
    statusProcess.running = true
  }

  function readClipboard() {
    clipboardText = ""
    clipProcess.command = ["wl-paste", "--no-newline"]
    clipProcess.running = true
  }


  function addTarget(target, stopped, savePath) {
    var value = String(target || "").trim()
    if (!Model.isAddableTarget(value)) {
      lastError = "Paste a magnet, a .torrent URL, or a .torrent file path."
      return
    }
    var args = []
    if (stopped) args.push("--stopped")
    var directory = String(savePath || "").trim()
    if (directory !== "") { args.push("--savepath"); args.push(directory) }
    args.push(value)
    runAction("add", args, stopped ? "Adding torrent (stopped)…" : "Adding torrent…")
  }

  function addUrl(url) { addTarget(url, false, "") }
  function startHash(hash) { runAction("start", [hash], "") }
  function stopHash(hash) { runAction("stop", [hash], "") }

  function toggleHash(hash) {
    var row = null
    for (var i = 0; i < torrents.length; i++) if (torrents[i].hash === hash) row = torrents[i]
    if (!row) return
    var bucket = Model.classifyState(row.state, row.progress)
    if (bucket === "paused" || bucket === "completed") startHash(hash)
    else stopHash(hash)
  }

  function toggleAll() {
    var live = Model.excludePending(torrents, magnetPendingHashes)
    if (live.length === 0) return
    var start = !Model.anyActive(live)
    for (var i = 0; i < live.length; i++) {
      var h = Model.torrentId(live[i])
      if (!h) continue
      if (start) startHash(h)
      else stopHash(h)
    }
  }

  function deleteHash(hash, withFiles) {
    var args = [hash]
    if (withFiles) args.push("--files")
    runAction("delete", args, withFiles ? "Deleting torrent and files…" : "Removing torrent…")
  }

  function loadFiles(hash) {
    files = []
    if (filesProcess.running) return
    filesConnectionKey = connectionKey
    filesProcess.command = helperCommand("files", [hash])
    filesProcess.running = true
  }

  function setPrio(hash, index, prio) { runAction("prio", [hash, index, prio], "") }
  function toggleTurtle() { runAction("turtle", [], "") }
  function setLimit(hash, kind, bytes) { runAction("limit", [hash, kind, bytes], "") }
  function toggleSequential(hash) { runAction("sequential", [hash], "") }
  function setShareRatio(hash, ratio) { runAction("sharelimit", [hash, ratio], "") }

  function installMagnetHandler() {
    if (magnetHandlerInstalled) return
    magnetHandlerInstalled = true
    runAction("magnet-install-handler", [], "")
  }

  function loadMagnetSnapshot() {
    if (magnetSnapProcess.running) return
    magnetSnapProcess.command = helperCommand("magnet-snapshot", [])
    magnetSnapProcess.running = true
  }

  function tickMagnet() {
    loadMagnetSnapshot()
    if (!ready) {
      var inbox = magnetInbox || []
      if (inbox.length > 0 && !inbox[0].notified && !magnetNotReadyNotified) {
        magnetNotReadyNotified = true
        notify("OmaqBT cannot reach the configured qBittorrent Web API")
      }
      return
    }
    magnetNotReadyNotified = false
    var now = Date.now()
    if ((magnetInbox || []).length > 0 && now >= magnetBackoffUntil && !magnetDrainQueued) {
      magnetDrainQueued = true
      runAction("magnet-drain", [], "Adding torrent from browser…")
    }
    stopPendingIfNeeded()
  }

  function stopPendingIfNeeded() {
    var p = magnetPending || []
    for (var i = 0; i < p.length; i++) {
      var hash = p[i] && p[i].hash
      if (!hash) continue
      var row = null
      for (var j = 0; j < torrents.length; j++) {
        if (Model.torrentId(torrents[j]) === hash || torrents[j].hash === hash) {
          row = torrents[j]
          break
        }
      }
      if (row && Model.pendingNeedsStop(row.state)) stopHash(hash)
    }
  }

  function dropPending(hash) { runAction("magnet-pending-drop", [hash], "") }
  function dropInboxCurrent() { runAction("magnet-inbox-drop", [], "") }

  function startPending(hash) {
    startHash(hash)
    dropPending(hash)
  }

  function cancelPending(hash) {
    deleteHash(hash, true)
    dropPending(hash)
  }

  onBaseUrlChanged: if (initialized) connectionChanged()
  onUsernameChanged: if (initialized) connectionChanged()
  Component.onCompleted: {
    initialized = true
    installMagnetHandler()
    refresh()
    loadMagnetSnapshot()
  }
  Timer {
    interval: root.refreshIntervalSec * 1000
    repeat: true
    running: !root.magnetWatching
    onTriggered: {
      root.refresh()
      root.loadMagnetSnapshot()
    }
  }

  Timer {
    interval: 250
    repeat: true
    running: root.magnetWatching
    onTriggered: {
      root.refresh()
      root.tickMagnet()
    }
  }

  Process {
    id: statusProcess
    running: false
    command: []
    stdout: StdioCollector { id: statusOut; waitForEnd: true }
    stderr: StdioCollector { id: statusErr; waitForEnd: true }
    onExited: function(exitCode) {
      if (root.statusConnectionKey !== root.connectionKey) {
        Qt.callLater(root.refresh)
        return
      }
      if (exitCode === 0) {
        root.applyStatus(statusOut.text)
      } else {
        root.clearRemoteState()
        root.lastError = Model.sanitizeError(statusErr.text || "Remote Web API is not reachable")
      }
    }
  }

  Process {
    id: notifyProcess
    running: false
    command: []
    // Best effort: a missing notify-send must not surface as a plugin error.
    onExited: function() { root.pumpNotifyQueue() }
  }

  Process {
    id: magnetSnapProcess
    running: false
    command: []
    stdout: StdioCollector { id: magnetSnapOut; waitForEnd: true }
    onExited: function(exitCode) {
      if (exitCode !== 0) return
      try {
        var snap = JSON.parse(String(magnetSnapOut.text || "{}"))
        root.magnetInbox = snap.inbox || []
        root.magnetPending = snap.pending || []
      } catch (e) {
        root.magnetInbox = []
        root.magnetPending = []
      }
    }
  }

  Process {
    id: clipProcess
    running: false
    command: []
    stdout: StdioCollector { id: clipOut; waitForEnd: true }
    onExited: function() { root.clipboardText = String(clipOut.text || "") }
  }

  Process {
    id: actionProcess
    running: false
    command: []
    stdout: StdioCollector { id: actionOut; waitForEnd: true }
    stderr: StdioCollector { id: actionErr; waitForEnd: true }
    onExited: function(exitCode) {
      var kind = root.actionName
      root.actionName = ""
      if (kind === "magnet-drain") root.magnetDrainQueued = false
      root.actionStatus = ""
      if (root.actionConnectionKey !== root.connectionKey) {
        Qt.callLater(root.refresh)
        return
      }
      if (exitCode !== 0) {
        root.lastError = Model.sanitizeError(actionErr.text || actionOut.text || "qBittorrent command failed")
        if (kind === "magnet-drain") root.magnetBackoffUntil = Date.now() + 2000
        root.pumpActionQueue()
        return
      }
      root.refresh()
      root.loadMagnetSnapshot()
      root.pumpActionQueue()
    }
  }

  Process {
    id: filesProcess
    running: false
    command: []
    stdout: StdioCollector { id: filesOut; waitForEnd: true }
    stderr: StdioCollector { id: filesErr; waitForEnd: true }
    onExited: function(exitCode) {
      if (root.filesConnectionKey !== root.connectionKey) {
        root.files = []
        return
      }
      if (exitCode !== 0) {
        root.lastError = Model.sanitizeError(filesErr.text || "Could not read files")
        root.files = []
        return
      }
      try { root.files = JSON.parse(String(filesOut.text || "[]")) }
      catch (error) { root.files = [] }
    }
  }
}
